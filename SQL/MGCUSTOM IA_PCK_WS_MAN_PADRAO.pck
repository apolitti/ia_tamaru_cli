CREATE OR REPLACE PACKAGE IA_PCK_WS_MAN_PADRAO IS

  V_ORG_TAB_IN_CODIGO MGADM.EST_MOVIMENTO.ORG_TAB_IN_CODIGO%TYPE     := 53;
  V_ORG_PAD_IN_CODIGO MGADM.EST_MOVIMENTO.ORG_PAD_IN_CODIGO%TYPE     := 1;	
  V_ORG_TAU_ST_CODIGO MGADM.EST_MOVIMENTO.ORG_TAU_ST_CODIGO%TYPE     := 'G';	

  V_ORD_TAB_IN_CODIGO  MGMAN.PRO_ORDENS.ORD_TAB_IN_CODIGO%TYPE       := 218;	
  V_ORD_SEQ_IN_CODIGO  MGMAN.PRO_ORDENS.ORD_SEQ_IN_CODIGO%TYPE;

	V_OPR_TAB_IN_CODIGO  MGMAN.PRO_OPERACOES.OPR_TAB_IN_CODIGO%TYPE    := 209;
  V_OPR_PAD_IN_CODIGO  MGMAN.PRO_OPERACOES.OPR_PAD_IN_CODIGO%TYPE;

  V_OPD_TAB_IN_CODIGO  MGMAN.PRO_CADOPERADOR.OPD_TAB_IN_CODIGO%TYPE  := 228;
  V_OPD_PAD_IN_CODIGO  MGMAN.PRO_CADOPERADOR.OPD_PAD_IN_CODIGO%TYPE;
	
	V_ATI_TAB_IN_CODIGO MGMAN.PRO_ATIVIDADE.ATI_TAB_IN_CODIGO%TYPE     := 204;
	V_ATI_PAD_IN_CODIGO	MGMAN.PRO_ATIVIDADE.ATI_PAD_IN_CODIGO%TYPE;
	
	V_MAQ_TAB_IN_CODIGO	MGMAN.PRO_MAQUINA.MAQ_TAB_IN_CODIGO%TYPE       := 207;
	V_MAQ_PAD_IN_CODIGO	MGMAN.PRO_MAQUINA.MAQ_PAD_IN_CODIGO%TYPE;
	
		
  CURSOR C_ORDEM (
    P_ORG_IN_CODIGO   NUMBER,
    P_ORD_IN_CODIGO_I NUMBER,
    P_ORD_IN_CODIGO_F NUMBER
  )
  IS (
    SELECT *
    FROM   MGMAN.PRO_ORDENS A
    WHERE  A.ORG_TAB_IN_CODIGO = 53
    AND    A.ORG_PAD_IN_CODIGO = 1
    AND    A.ORG_IN_CODIGO     = P_ORG_IN_CODIGO
    AND    A.ORG_TAU_ST_CODIGO = 'G'
    AND    A.ORD_TAB_IN_CODIGO = 218
    AND    A.ORD_SEQ_IN_CODIGO = 1
    AND    A.ORD_IN_CODIGO     BETWEEN P_ORD_IN_CODIGO_I AND P_ORD_IN_CODIGO_F
  );
  TYPE T_ORDEM IS TABLE OF C_ORDEM%ROWTYPE;
 
  PROCEDURE P_MAQUINA_ORDEM_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );

  CURSOR C_MAQUINA (
    P_ORG_IN_CODIGO   NUMBER,
    P_MAQ_IN_CODIGO_I NUMBER,
    P_MAQ_IN_CODIGO_F NUMBER
  )
  IS (
    SELECT *
    FROM   (
             SELECT A.MAQ_IN_CODIGO,
                    A.MAQ_ST_NOME
             FROM   MGMAN.PRO_MAQUINA   A
             WHERE  A.MAQ_IN_CODIGO     BETWEEN NVL(P_MAQ_IN_CODIGO_I, A.MAQ_IN_CODIGO) AND NVL(P_MAQ_IN_CODIGO_F, A.MAQ_IN_CODIGO)
             AND    ROWNUM              <= 5
             ORDER
             BY     A.MAQ_IN_CODIGO ASC
           )
  );
  TYPE T_MAQUINA IS TABLE OF C_MAQUINA%ROWTYPE;

  PROCEDURE P_MAQUINA_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_OPERADOR_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
  
  PROCEDURE P_ATIVIDADE_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );	
	
	PROCEDURE P_REFUGO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_OPERACAO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_ORDEM_OPERACAO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_ORDEM_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_APONTAMENTO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_APONTAMENTO_EXE (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_APONTAMENTO_DEL (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );

	
	PROCEDURE P_CMAQUINA_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  );
	
	PROCEDURE P_EVENTO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  ); 

END IA_PCK_WS_MAN_PADRAO;
/
CREATE OR REPLACE PACKAGE BODY IA_PCK_WS_MAN_PADRAO IS

  PROCEDURE P_CARREGA_PADROES (P_ORG_IN_CODIGO MGGLO.GLO_ORGANIZACAO.AGN_IN_CODIGO%TYPE) 
	IS
	BEGIN
    
		V_ORD_SEQ_IN_CODIGO := MGGLO.PCK_MEGA.ACHASEQUENCIADATABELA(P_ORG_IN_CODIGO, V_ORD_TAB_IN_CODIGO, TRUNC(SYSDATE));	
    V_OPR_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(P_ORG_IN_CODIGO, V_OPR_TAB_IN_CODIGO, TRUNC(SYSDATE));		  	
    V_OPD_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(P_ORG_IN_CODIGO, V_OPD_TAB_IN_CODIGO, TRUNC(SYSDATE));		
		V_ATI_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(P_ORG_IN_CODIGO, V_ATI_TAB_IN_CODIGO, TRUNC(SYSDATE));
		V_MAQ_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(P_ORG_IN_CODIGO, V_MAQ_TAB_IN_CODIGO, TRUNC(SYSDATE));
				
	END;
 
  PROCEDURE P_MAQUINA_ORDEM_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_MAQ_IN_CODIGO_I    NUMBER;
		V_MAQ_IN_CODIGO_F    NUMBER;
    V_CEL_IN_CODIGO_I    NUMBER;
	  V_CEL_IN_CODIGO_F    NUMBER;
		V_RANK               NUMBER;
  BEGIN
        
    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO_I')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO_F')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CEL_IN_CODIGO_I')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CEL_IN_CODIGO_F')),					 
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/RANK'))
    INTO   V_ORG_IN_CODIGO,
					 V_MAQ_IN_CODIGO_I,
					 V_MAQ_IN_CODIGO_F,
					 V_CEL_IN_CODIGO_I,
					 V_CEL_IN_CODIGO_F,
					 V_RANK			 
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

    P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);	
	
    OPEN P_RESULT FOR
    SELECT X.*
		FROM   (
						SELECT X.*,		
									 RANK() OVER (PARTITION BY X.maq_in_codigo ORDER BY X.MAQ_IN_CODIGO ASC, X.SEQ ASC) AS "RANK"
						FROM   (
										SELECT A.SEQ,
										       A.MAQ_IN_CODIGO,
													 A.MAQ_ST_NOME,
													 A.ORD_IN_CODIGO,
													 A.ORD_IN_CODIGO AS ORD_ST_ALTERNATIVO,
													 A.ORD_IN_CODIGO_PAI,
													 TO_CHAR(A.ORD_DT_RECEBIMENTO, 'DD/MM/YYYY') AS ORD_DT_RECEBIMENTO,
													 A.POD_RE_QTDENECESSARIA,
													 A.POD_RE_QTDEREAL,
													 A.PLF_IN_SQOPERACAO,
													 A.PRO_IN_CODIGO,
													 A.PRO_ST_ALTERNATIVO,
													 A.PRO_ST_DESCRICAO,
													 A.CLI_IN_CODIGO   AS ORD_ST_COMPLEMENTO,
													 A.GRU_ST_NOME,
													 A.ORD_CH_STATUS,
													 A.ORD_ST_STATUS,
													 A.POD_RE_TEMPOEXEC_PREV_TOT,
													 A.POD_RE_TEMPOEXEC_PREV_UNIT,
													 A.POD_BO_BLOQUEIA_APT_SEQ,
													 A.PRC_ST_DESCRICAO,
													 TO_CHAR(A.APT_DT_INICIO,'DD/MM/YYYY HH24:MI') AS APT_DT_INICIO,
													 A.MAQ_CH_STATUS,
													 A.MAQ_ST_STATUS					 
										FROM   MGCUSTOM.APT_VW_OPERACOES_ORDEM_SLD A
										WHERE  A.ORG_IN_CODIGO = V_ORG_IN_CODIGO
										AND    A.MAQ_IN_CODIGO BETWEEN V_MAQ_IN_CODIGO_I AND V_MAQ_IN_CODIGO_F
										AND    A.CEL_IN_CODIGO BETWEEN V_CEL_IN_CODIGO_I AND V_CEL_IN_CODIGO_F		
										ORDER BY A.MAQ_IN_CODIGO ASC, A.SEQ ASC
									)	X
				   ) X
	  WHERE  X.RANK <= NVL(V_RANK, X.RANK)
		;

  END;

  PROCEDURE P_MAQUINA_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_MAQ_IN_CODIGO      MGMAN.PRO_MAQUINA.MAQ_IN_CODIGO%TYPE;
		V_MAQ_ST_NOME        MGMAN.PRO_MAQUINA.MAQ_ST_NOME%TYPE;
  BEGIN
        
    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO')),
					 TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_ST_NOME'))
    INTO   V_ORG_IN_CODIGO,
           V_MAQ_IN_CODIGO,
					 V_MAQ_ST_NOME
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

    P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);

    OPEN P_RESULT FOR
    SELECT A.MAQ_IN_CODIGO,
		       A.MAQ_ST_NOME
    FROM   MGMAN.PRO_MAQUINA A
		WHERE  A.MAQ_TAB_IN_CODIGO = V_MAQ_TAB_IN_CODIGO 
    AND    A.MAQ_PAD_IN_CODIGO = V_MAQ_PAD_IN_CODIGO 
		AND    A.MAQ_IN_CODIGO     = NVL(V_MAQ_IN_CODIGO, A.MAQ_IN_CODIGO)
		/*AND    (
		         V_MAQ_ST_NOME IS NULL
						 OR 
						 (
               V_MAQ_ST_NOME IS NOT NULL
							 AND 
							 A.MAQ_ST_NOME LIKE '%' || V_MAQ_ST_NOME || '%'
						 )
		       )*/
    ;

  END;  
	
	PROCEDURE P_OPERADOR_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_OPD_ST_ALTERNATIVO MGMAN.PRO_CADOPERADOR.OPD_ST_ALTERNATIVO%TYPE;		
  BEGIN
        
    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/OPD_ST_ALTERNATIVO'))
    INTO   V_ORG_IN_CODIGO,
           V_OPD_ST_ALTERNATIVO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

    P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);

    OPEN P_RESULT FOR
    SELECT A.OPD_IN_CODIGO, 
		       A.OPD_ST_ALTERNATIVO, 
					 A.OPD_ST_DESCRICAO
    FROM   MGMAN.PRO_CADOPERADOR A
		WHERE  A.OPD_TAB_IN_CODIGO  = V_OPD_TAB_IN_CODIGO
		AND    A.OPD_PAD_IN_CODIGO  = V_OPD_PAD_IN_CODIGO
		AND    A.OPD_ST_ALTERNATIVO = NVL(V_OPD_ST_ALTERNATIVO, A.OPD_ST_ALTERNATIVO)
    ;

  END;
	
  PROCEDURE P_ATIVIDADE_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;		
		V_ATI_IN_CODIGO      MGMAN.PRO_ATIVIDADE.ATI_IN_CODIGO%TYPE;
		V_ATI_CH_PRODUTIVA   MGMAN.PRO_ATIVIDADE.ATI_CH_PRODUTIVA%TYPE;
		V_ATI_ST_NOME        MGMAN.PRO_ATIVIDADE.ATI_ST_NOME%TYPE;
  BEGIN
        
    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ATI_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ATI_CH_PRODUTIVA')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ATI_ST_NOME'))					 
    INTO   V_ORG_IN_CODIGO,
					 V_ATI_IN_CODIGO,
					 V_ATI_CH_PRODUTIVA,
					 V_ATI_ST_NOME
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

    P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);

    OPEN P_RESULT FOR
    SELECT A.ATI_IN_CODIGO,
		       A.ATI_ST_NOME,
					 A.ATI_CH_PRODUTIVA,
					 A.ATI_BO_APONTAMENTO,
					 A.ATI_BO_FIELDORDEM,
					 A.ATI_BO_FIELDOPERADOR,
					 A.ATI_BO_FIELDCTRABALHO,
					 A.ATI_BO_FIELDMAQUINA,
					 A.ATI_BO_FIELDTPINICIAL,
					 A.ATI_BO_FIELDTPFINAL
    FROM   MGMAN.PRO_ATIVIDADE A
		WHERE  A.ATI_TAB_IN_CODIGO = V_ATI_TAB_IN_CODIGO
		AND    A.ATI_PAD_IN_CODIGO = V_ATI_PAD_IN_CODIGO
		AND    A.ATI_IN_CODIGO     = NVL(V_ATI_IN_CODIGO, A.ATI_IN_CODIGO)
		AND    A.ATI_CH_PRODUTIVA  = NVL(V_ATI_CH_PRODUTIVA, A.ATI_CH_PRODUTIVA)
		AND    (
		         V_ATI_ST_NOME IS NULL
						 OR 
						 (
						   V_ATI_ST_NOME IS NOT NULL
							 AND 
							 A.ATI_ST_NOME LIKE '%'|| V_ATI_ST_NOME ||'%'
						 )
		       )
    ;

  END;	
	
  PROCEDURE P_REFUGO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
		V_REF_TAB_IN_CODIGO  MGADM.EST_OCORRENCIAREFUGO.REF_TAB_IN_CODIGO%TYPE;
    V_REF_PAD_IN_CODIGO  MGADM.EST_OCORRENCIAREFUGO.REF_PAD_IN_CODIGO%TYPE;
    V_REF_IN_CODIGO      MGADM.EST_OCORRENCIAREFUGO.REF_IN_CODIGO%TYPE;		
  BEGIN
        
    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/REF_IN_CODIGO'))
    INTO   V_ORG_IN_CODIGO,
           V_REF_IN_CODIGO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));


  	V_REF_TAB_IN_CODIGO := 137;
    V_REF_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(V_ORG_IN_CODIGO, V_REF_TAB_IN_CODIGO, TRUNC(SYSDATE));

    --RAISE_APPLICATION_ERROR(-20000, V_OPD_ST_ALTERNATIVO);

    OPEN P_RESULT FOR
		SELECT A.REF_IN_CODIGO,
		       A.REF_ST_NOME
		FROM   MGADM.EST_OCORRENCIAREFUGO A
		WHERE  A.REF_TAB_IN_CODIGO = V_REF_TAB_IN_CODIGO
    AND    A.REF_PAD_IN_CODIGO = V_REF_PAD_IN_CODIGO
		AND    A.REF_IN_CODIGO     = NVL(V_REF_IN_CODIGO, A.REF_IN_CODIGO)
    ;

  END;	
	
	
	
	PROCEDURE P_OPERACAO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_FIL_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;				
    V_ORD_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;		
    V_PLF_IN_SQOPERACAO  MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;
		
  BEGIN

    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO'),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/FIL_IN_CODIGO')),					 
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/PLF_IN_SQOPERACAO'))
    INTO   V_ORG_IN_CODIGO,
		       V_ORD_IN_CODIGO,
					 V_FIL_IN_CODIGO,
           V_PLF_IN_SQOPERACAO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

		
		P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);
		   
    OPEN P_RESULT FOR
    SELECT B.ORG_IN_CODIGO,
		       B.ORD_SEQ_IN_CODIGO,
					 B.ORD_IN_CODIGO,
					 B.PLF_IN_SQOPERACAO,
		       A.OPR_IN_CODIGO,
		       A.OPR_ST_OPERACAO,
					 B.MAQ_IN_CODIGO,
					 C.MAQ_ST_NOME,
					 D.OPR_IN_CODIGO,
					 NVL(D.OPR_BO_EDTTEMPOINICIAL, 'N') AS OPR_BO_EDTTEMPOINICIAL,
					 NVL(D.OPR_BO_EDTTEMPOFINAL, 'N') AS OPR_BO_EDTTEMPOFINAL,
					 NVL(D.OPR_BO_APTELEUNICO, 'N') AS OPR_BO_APTELEUNICO
    FROM   MGMAN.PRO_OPERACOES       A,
		       MGMAN.PRO_PROG_ORDEM      B,
					 MGMAN.PRO_MAQUINA         C,
					 MGMAN.PRO_OPERACOESCMPESP D
		WHERE  A.OPR_TAB_IN_CODIGO = B.OPR_TAB_IN_CODIGO
    AND    A.OPR_PAD_IN_CODIGO = B.OPR_PAD_IN_CODIGO
    AND    A.OPR_IN_CODIGO     = B.OPR_IN_CODIGO  
		AND    B.MAQ_TAB_IN_CODIGO = C.MAQ_TAB_IN_CODIGO
    AND    B.MAQ_PAD_IN_CODIGO = C.MAQ_PAD_IN_CODIGO
    AND    B.MAQ_IN_CODIGO     = C.MAQ_IN_CODIGO    

    AND    A.OPR_TAB_IN_CODIGO  = D.OPR_TAB_IN_CODIGO (+)
    AND    A.OPR_PAD_IN_CODIGO  = D.OPR_PAD_IN_CODIGO (+)
    AND    A.OPR_IN_CODIGO      = D.OPR_IN_CODIGO     (+)

    AND    B.ORG_TAB_IN_CODIGO  = V_ORG_TAB_IN_CODIGO
		AND    B.ORG_PAD_IN_CODIGO  = V_ORG_PAD_IN_CODIGO
		AND    B.ORG_IN_CODIGO      = V_ORG_IN_CODIGO
		AND    B.ORG_TAU_ST_CODIGO  = V_ORG_TAU_ST_CODIGO
		AND    B.FIL_IN_CODIGO      = V_FIL_IN_CODIGO
		AND    B.ORD_TAB_IN_CODIGO  = V_ORD_TAB_IN_CODIGO
		AND    B.ORD_SEQ_IN_CODIGO  = V_ORD_SEQ_IN_CODIGO
		AND    B.ORD_IN_CODIGO      = V_ORD_IN_CODIGO
		AND    B.PLF_IN_SQOPERACAO  = V_PLF_IN_SQOPERACAO    
		;

  END;

	PROCEDURE P_ORDEM_OPERACAO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_FIL_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;				
    V_ORD_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;		
    V_PLF_IN_SQOPERACAO  MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;
		V_MAQ_IN_CODIGO      MGMAN.PRO_MAQUINA.MAQ_IN_CODIGO%TYPE;
		V_APT_DT_APONTAMENTO DATE;
		V_APT_DT_ENCERRAMENTO DATE;
  BEGIN

    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO'),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/FIL_IN_CODIGO')),					 
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/PLF_IN_SQOPERACAO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_APONTAMENTO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_ENCERRAMENTO'))	
					 			 
    INTO   V_ORG_IN_CODIGO,
		       V_ORD_IN_CODIGO,
					 V_FIL_IN_CODIGO,
           V_PLF_IN_SQOPERACAO,
					 V_MAQ_IN_CODIGO,
					 V_APT_DT_APONTAMENTO,
					 V_APT_DT_ENCERRAMENTO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

		
		P_CARREGA_PADROES(P_ORG_IN_CODIGO => V_ORG_IN_CODIGO);
		   
    OPEN P_RESULT FOR
    SELECT A.SEQ,
		
		       A.ORD_IN_CODIGO,
		       A.ORD_IN_CODIGO_PAI,
           A.ORD_ST_ALTERNATIVO,
					 TO_CHAR(A.ORD_DT_RECEBIMENTO, 'DD/MM/YYYY') AS ORD_DT_RECEBIMENTO,
					 
					 A.PRO_ST_ALTERNATIVO,
					 A.PRO_ST_DESCRICAO,
					 
					 A.POD_RE_QTDENECESSARIA, 		
					 A.POD_RE_QTDEREAL,
					 A.POD_RE_QTDE_SALDO,
					 --A.POD_BO_BLOQUEIA_APT_SEQ,					 
           'N' AS POD_BO_BLOQUEIA_APT_SEQ,					 
					 DECODE(A.POD_BO_BLOQUEIA_APT_SEQ, 'S', 'Sim', 'Não') AS POD_ST_BLOQUEIA_APT_SEQ,
					 A.POD_BO_VALIDA_SALDO,		 
					 
					 A.OPR_IN_CODIGO,
					 A.PLF_IN_SQOPERACAO,
           A.ORD_IN_PRIORIDADE,
					 A.ORD_ST_SEMANA,
					 A.PRC_ST_DESCRICAO,
					 A.ORD_ST_STATUS,
					 A.POD_RE_TEMPOEXEC_PREV_UNIT,
					 A.POD_RE_TEMPOEXEC_PREV_TOT,
					 A.APT_DT_INICIO
					 
					 
    FROM   MGCUSTOM.APT_VW_OPERACOES_ORDEM_SLD A
    WHERE  A.ORG_IN_CODIGO     = V_ORG_IN_CODIGO
		AND    A.ORD_IN_CODIGO     = NVL(V_ORD_IN_CODIGO, A.ORD_IN_CODIGO) 
		AND    A.PLF_IN_SQOPERACAO = NVL(V_PLF_IN_SQOPERACAO, A.PLF_IN_SQOPERACAO)
		AND    A.MAQ_IN_CODIGO     = NVL(V_MAQ_IN_CODIGO, A.MAQ_IN_CODIGO)
		AND    A.ORD_DT_RECEBIMENTO BETWEEN NVL(V_APT_DT_APONTAMENTO, A.ORD_DT_RECEBIMENTO) AND NVL(V_APT_DT_ENCERRAMENTO, A.ORD_DT_RECEBIMENTO)
		;

  END;	
		
	PROCEDURE P_ORDEM_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
    V_ORD_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;		
  BEGIN

    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
		
    SELECT TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO')),
           TRIM(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO'))
    INTO   V_ORG_IN_CODIGO,
           V_ORD_IN_CODIGO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

  	V_ORD_TAB_IN_CODIGO := 218;
    V_ORD_SEQ_IN_CODIGO := MGGLO.PCK_MEGA.ACHASEQUENCIADATABELA(V_ORG_IN_CODIGO, V_ORD_TAB_IN_CODIGO, TRUNC(SYSDATE));

    OPEN P_RESULT FOR
    SELECT A.ORD_IN_CODIGO,
		       A.ORD_ST_ALTERNATIVO,
					 A.ORD_DT_RECEBIMENTO,
		       A.PRO_IN_CODIGO,
					 B.PRO_ST_ALTERNATIVO,
					 B.PRO_ST_DESCRICAO,
					 MGCUSTOM.CUS_F_ORDEM_PAI(A.ORG_IN_CODIGO, A.ORD_SEQ_IN_CODIGO, A.ORD_IN_CODIGO) AS ORD_IN_CODIGO_PAI
    FROM   MGMAN.PRO_ORDENS   A,
		       MGADM.EST_PRODUTOS B
		WHERE  A.PRO_TAB_IN_CODIGO = B.PRO_TAB_IN_CODIGO
    AND    A.PRO_PAD_IN_CODIGO = B.PRO_PAD_IN_CODIGO
    AND    A.PRO_IN_CODIGO     = B.PRO_IN_CODIGO    
		AND    A.ORD_TAB_IN_CODIGO = V_ORD_TAB_IN_CODIGO
    AND    A.ORD_SEQ_IN_CODIGO = V_ORD_SEQ_IN_CODIGO
		AND    A.ORD_IN_CODIGO     = V_ORD_IN_CODIGO
		;

  END;
	
  PROCEDURE P_APONTAMENTO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
		
    V_ORD_IN_CODIGO      MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;		
    V_ORD_IN_CODIGO_PAI  MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;		
		V_PLF_IN_SQOPERACAO  MGMAN.PRO_OPERACOES.OPR_IN_CODIGO%TYPE;
		
		V_OPD_ST_ALTERNATIVO MGMAN.PRO_CADOPERADOR.OPD_ST_ALTERNATIVO%TYPE;	
		
		V_APT_MAQ_IN_CODIGO MGCUSTOM.APT_APONTAORDEM.APT_MAQ_IN_CODIGO%TYPE;	
    V_APT_CMAQ_IN_CODIGO MGCUSTOM.APT_APONTAORDEM.APT_CMAQ_IN_CODIGO%TYPE;
		
    V_APT_DT_APONTAMENTO  MGCUSTOM.APT_APONTAORDEM.APT_DT_APONTAMENTO%TYPE;
		V_APT_DT_ENCERRAMENTO	MGCUSTOM.APT_APONTAORDEM.APT_DT_ENCERRAMENTO%TYPE;
		
		V_ATI_CH_PRODUTIVA    MGMAN.PRO_ATIVIDADE.ATI_CH_PRODUTIVA%TYPE;
		
		V_APT_BO_ENCERRADO VARCHAR2(1);
		
		V_APT_CH_STATUS VARCHAR2(1);
						
  BEGIN

    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO'),
           EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO'),
           EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO_PAI'),					 
           EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/PLF_IN_SQOPERACAO'),					 					 
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO'),	
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CMAQ_IN_CODIGO'),					 				 
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/OPD_ST_ALTERNATIVO'),
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_BO_ENCERRADO'),
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_APONTAMENTO'),
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_ENCERRAMENTO'),
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_CH_STATUS'),
					 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ATI_CH_PRODUTIVA')
    INTO   V_ORG_IN_CODIGO,
           V_ORD_IN_CODIGO,
           V_ORD_IN_CODIGO_PAI,					 
					 V_PLF_IN_SQOPERACAO,
					 V_APT_MAQ_IN_CODIGO,
					 V_APT_CMAQ_IN_CODIGO,					 
					 V_OPD_ST_ALTERNATIVO,
					 V_APT_BO_ENCERRADO,
					 V_APT_DT_APONTAMENTO,
					 V_APT_DT_ENCERRAMENTO,
					 V_APT_CH_STATUS,
					 V_ATI_CH_PRODUTIVA
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));
		
		P_CARREGA_PADROES(V_ORG_IN_CODIGO);
    		    
		OPEN P_RESULT FOR
    SELECT A.APT_IN_SEQUENCIA,
		       A.ORD_IN_CODIGO,
					 A.PLF_IN_SQOPERACAO,
					 A.APT_CMAQ_IN_CODIGO,
					 DECODE(A.APT_CH_STATUS, 'A', 'Aberto', 'E', 'Encerrado', '') AS APT_ST_STATUS,
					 TO_CHAR(A.APT_DT_APONTAMENTO, 'DD/MM/YYYY') AS APT_DT_APONTAMENTO,
					 SUBSTR(TRIM(MGMAN.MAN_PCK_UTIL.F_CONVERTEHORAS(A.APT_RE_TEMPOINICIAL)),1,5) AS APT_HR_TEMPOINICIAL,
		       TO_CHAR(A.APT_DT_ENCERRAMENTO, 'DD/MM/YYYY') AS APT_DT_ENCERRAMENTO,
					 SUBSTR(TRIM(MGMAN.MAN_PCK_UTIL.F_CONVERTEHORAS(A.APT_RE_TEMPOFINAL)),1,5) AS APT_HR_TEMPOFINAL,
					 A.APT_RE_QUANTIDADE,
					 A.APT_RE_QUANTIDADEREF,
					 A.APT_REF_IN_CODIGO,
					 E.REF_ST_NOME,
					 MGCUSTOM.CUS_F_ORDEM_PAI(A.ORG_IN_CODIGO, A.ORD_SEQ_IN_CODIGO, A.ORD_IN_CODIGO) AS ORD_IN_CODIGO_PAI,
					 G.OPR_BO_APTELEUNICO,
					 G.OPR_BO_EDTTEMPOINICIAL,
           G.OPR_BO_EDTTEMPOFINAL,
					 H.ATI_CH_PRODUTIVA					 			
    FROM   MGCUSTOM.APT_APONTAORDEM   A		
		LEFT
		JOIN   MGMAN.PRO_CADOPERADOR      B
		ON     (       
		             A.OPD_TAB_IN_CODIGO  = B.OPD_TAB_IN_CODIGO 
					   AND A.OPD_PAD_IN_CODIGO  = B.OPD_PAD_IN_CODIGO 
						 AND A.OPD_IN_CODIGO      = B.OPD_IN_CODIGO     
					 )
		LEFT
		JOIN   MGMAN.PRO_MAQUINA          C
		ON     (
             A.APT_MAQ_IN_CODIGO  = C.MAQ_IN_CODIGO
		       )
		LEFT
		JOIN   MGMAN.PRO_PERTMAQUINAS     D
    ON     (
    		         A.APT_MAQ_IN_CODIGO  = D.CTR_IN_CODIGO 
		         AND A.APT_CMAQ_IN_CODIGO = D.CMAQ_IN_CODIGO			
		       )
		LEFT
		JOIN   MGADM.EST_OCORRENCIAREFUGO E
    ON     (
		         A.APT_REF_IN_CODIGO  = E.REF_IN_CODIGO
           )
		LEFT
		JOIN   MGMAN.PRO_PROG_ORDEM       F
    ON     (
					 	     A.ORG_TAB_IN_CODIGO  = F.ORG_TAB_IN_CODIGO
				 		 AND A.ORG_PAD_IN_CODIGO  = F.ORG_PAD_IN_CODIGO
				 		 AND A.ORG_IN_CODIGO      = F.ORG_IN_CODIGO    
				 		 AND A.ORG_TAU_ST_CODIGO  = F.ORG_TAU_ST_CODIGO
			 			 AND A.ORD_TAB_IN_CODIGO  = F.ORD_TAB_IN_CODIGO
						 AND A.ORD_SEQ_IN_CODIGO  = F.ORD_SEQ_IN_CODIGO
						 AND A.ORD_IN_CODIGO      = F.ORD_IN_CODIGO    
						 AND A.PLF_IN_SQOPERACAO  = F.PLF_IN_SQOPERACAO
		
		)
		LEFT
		JOIN   MGMAN.PRO_OPERACOESCMPESP  G
    ON     (
						     F.OPR_TAB_IN_CODIGO  = G.OPR_TAB_IN_CODIGO 
						 AND F.OPR_PAD_IN_CODIGO  = G.OPR_PAD_IN_CODIGO 
						 AND F.OPR_IN_CODIGO      = G.OPR_IN_CODIGO    						
		       )
		LEFT
		JOIN   MGMAN.PRO_ATIVIDADE        H
		ON     (
		         A.APT_ATI_IN_CODIGO  = H.ATI_IN_CODIGO		
		       )
		WHERE  A.ORD_TAB_IN_CODIGO  = V_ORD_TAB_IN_CODIGO 
    AND    A.ORD_SEQ_IN_CODIGO  = V_ORD_SEQ_IN_CODIGO 
		AND    (V_ORD_IN_CODIGO IS NULL OR (V_ORD_IN_CODIGO IS NOT NULL AND A.ORD_IN_CODIGO = V_ORD_IN_CODIGO))
		
		
		AND    (V_PLF_IN_SQOPERACAO IS NULL OR (V_PLF_IN_SQOPERACAO IS NOT NULL AND A.PLF_IN_SQOPERACAO = V_PLF_IN_SQOPERACAO))		
		AND    (V_OPD_ST_ALTERNATIVO IS NULL OR (V_OPD_ST_ALTERNATIVO IS NOT NULL AND B.OPD_ST_ALTERNATIVO = V_OPD_ST_ALTERNATIVO))		
		AND    (V_APT_MAQ_IN_CODIGO IS NULL OR (V_APT_MAQ_IN_CODIGO IS NOT NULL AND A.APT_MAQ_IN_CODIGO = V_APT_MAQ_IN_CODIGO))		
		AND    (V_ATI_CH_PRODUTIVA IS NULL OR (V_ATI_CH_PRODUTIVA IS NOT NULL AND H.ATI_CH_PRODUTIVA = V_ATI_CH_PRODUTIVA))
		AND    (V_APT_CH_STATUS IS NULL OR (V_APT_CH_STATUS IS NOT NULL AND A.APT_CH_STATUS = V_APT_CH_STATUS))
		AND    (V_APT_CMAQ_IN_CODIGO IS NULL OR (V_APT_CMAQ_IN_CODIGO IS NOT NULL AND A.APT_CMAQ_IN_CODIGO = V_APT_CMAQ_IN_CODIGO))
									
		AND    (
		         V_ORD_IN_CODIGO_PAI IS NULL
						 OR 
						 (
  		         V_ORD_IN_CODIGO_PAI IS NOT NULL						   
							 AND 
							 MGCUSTOM.CUS_F_ORDEM_PAI(A.ORG_IN_CODIGO, A.ORD_SEQ_IN_CODIGO, A.ORD_IN_CODIGO) = V_ORD_IN_CODIGO_PAI
						 )
		       )
		
		AND    (
		         V_APT_DT_APONTAMENTO IS NULL 
						 OR
						 (
						   V_APT_DT_APONTAMENTO IS NOT NULL
							 AND 
							 A.APT_DT_APONTAMENTO >= V_APT_DT_APONTAMENTO
						 )
					 )
					 
    AND    (
		         V_APT_DT_ENCERRAMENTO IS NULL 
						 OR
						 (
						   V_APT_DT_ENCERRAMENTO IS NOT NULL
							 AND 
							 NVL(A.APT_DT_ENCERRAMENTO, TRUNC(SYSDATE)) <= V_APT_DT_ENCERRAMENTO
						 )
					 )					 
					
		AND    (
		         V_APT_BO_ENCERRADO IS NULL
						 OR 
						 V_APT_BO_ENCERRADO = 'S' AND A.APT_CH_STATUS = 'E' --A.APT_DT_ENCERRAMENTO IS NOT NULL
						 OR
						 V_APT_BO_ENCERRADO = 'N' AND A.APT_CH_STATUS = 'A' --A.APT_DT_ENCERRAMENTO IS NULL
		       )
	  ORDER 
		BY     A.APT_DT_APONTAMENTO DESC, A.APT_RE_TEMPOINICIAL DESC , A.APT_DT_ENCERRAMENTO DESC, A.APT_RE_TEMPOFINAL DESC
		;

  END;	
	/*P_PARAMS := '
	  <PARAMETROS>
      <ORG_IN_CODIGO>10</ORG_IN_CODIGO>
      <ORD_IN_CODIGO>105</ORD_IN_CODIGO>
			<PLF_IN_SQOPERACAO>30</PLF_IN_SQOPERACAO>
			<MAQ_IN_CODIGO>10101</MAQ_IN_CODIGO>
			<CMAQ_IN_CODIGO>1</CMAQ_IN_CODIGO>
			<OPD_ST_ALTERNATIVO>1</OPD_ST_ALTERNATIVO>
			<APT_DT_APONTAMENTO>05/07/2019</APT_DT_APONTAMENTO>
			<APT_RE_TEMPOINICIAL>10:00:00</APT_RE_TEMPOINICIAL>
			<APT_RE_TEMPOFINAL></APT_RE_TEMPOFINAL>		
			<APT_IN_SEQUENCIA></APT_IN_SEQUENCIA>	
    </PARAMETROS>
';*/
  PROCEDURE P_APONTAMENTO_EXE (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		
		V_OPD_ST_ALTERNATIVO MGMAN.PRO_CADOPERADOR.OPD_ST_ALTERNATIVO%TYPE;	
						
		V_APT                 MGCUSTOM.APT_APONTAORDEM%ROWTYPE;
    V_APT_HR_TEMPOINICIAL VARCHAR(10);
		V_APT_HR_TEMPOFINAL   VARCHAR(10);

    V_OPR_BO_APTELEUNICO     MGMAN.PRO_OPERACOESCMPESP.OPR_BO_APTELEUNICO%TYPE;	
    V_OPR_BO_EDTTEMPOINICIAL MGMAN.PRO_OPERACOESCMPESP.OPR_BO_EDTTEMPOINICIAL%TYPE;	
    V_OPR_BO_EDTTEMPOFINAL   MGMAN.PRO_OPERACOESCMPESP.OPR_BO_EDTTEMPOFINAL%TYPE;			
    V_OPR_IN_CODIGO	         MGMAN.PRO_PROG_ORDEM.OPR_IN_CODIGO%TYPE;
		
		V_ATI_CH_PRODUTIVA       MGMAN.PRO_ATIVIDADE.ATI_CH_PRODUTIVA%TYPE;
		
		V_CODIGO   NUMBER;
    V_MENSAGEM VARCHAR2(500);
								
  BEGIN
    BEGIN
			V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
			SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/FIL_IN_CODIGO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORD_IN_CODIGO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/PLF_IN_SQOPERACAO'),					 					 
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/MAQ_IN_CODIGO'),	
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CMAQ_IN_CODIGO'),					 				 
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/OPD_ST_ALTERNATIVO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_APONTAMENTO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_IN_SEQUENCIA'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_RE_TEMPOINICIAL'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_DT_ENCERRAMENTO'),					 					 
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_RE_TEMPOFINAL'),
						 NVL(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_RE_QUANTIDADE'),0),
						 NVL(EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_RE_QUANTIDADEREF'),0),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_REF_IN_CODIGO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_CH_STATUS'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ATI_IN_CODIGO'),
						 EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_ST_OBSERVACAO')
			INTO   V_APT.ORG_IN_CODIGO,
			       V_APT.FIL_IN_CODIGO,
						 V_APT.ORD_IN_CODIGO,
						 V_APT.PLF_IN_SQOPERACAO,
						 V_APT.APT_MAQ_IN_CODIGO,
						 V_APT.APT_CMAQ_IN_CODIGO,					 
						 V_OPD_ST_ALTERNATIVO,
						 V_APT.APT_DT_APONTAMENTO,
						 V_APT.APT_IN_SEQUENCIA,
						 V_APT_HR_TEMPOINICIAL,
						 V_APT.APT_DT_ENCERRAMENTO,
						 V_APT_HR_TEMPOFINAL,
						 V_APT.APT_RE_QUANTIDADE,
						 V_APT.APT_RE_QUANTIDADEREF,
						 V_APT.APT_REF_IN_CODIGO,
						 V_APT.APT_CH_STATUS,
						 V_APT.APT_ATI_IN_CODIGO,
						 V_APT.APT_ST_OBSERVACAO
			FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));

			V_APT.ORG_TAB_IN_CODIGO := 53;
			V_APT.ORG_PAD_IN_CODIGO := 1;
			V_APT.ORG_TAU_ST_CODIGO := 'G';
			
			V_APT.ORD_TAB_IN_CODIGO := 218;
			V_APT.ORD_SEQ_IN_CODIGO := MGGLO.PCK_MEGA.ACHASEQUENCIADATABELA(V_APT.ORG_IN_CODIGO, V_APT.ORD_TAB_IN_CODIGO, TRUNC(SYSDATE));
			
			V_APT.OPD_TAB_IN_CODIGO := 228;
			V_APT.OPD_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(V_APT.ORG_IN_CODIGO, V_APT.OPD_TAB_IN_CODIGO, TRUNC(SYSDATE));

			IF V_OPD_ST_ALTERNATIVO IS NOT NULL THEN
				BEGIN
					SELECT A.OPD_IN_CODIGO
					INTO   V_APT.OPD_IN_CODIGO
					FROM   MGMAN.PRO_CADOPERADOR A
					WHERE  A.OPD_TAB_IN_CODIGO  = V_APT.OPD_TAB_IN_CODIGO 
					AND    A.OPD_PAD_IN_CODIGO  = V_APT.OPD_PAD_IN_CODIGO 
					AND    A.OPD_ST_ALTERNATIVO = V_OPD_ST_ALTERNATIVO;
				EXCEPTION
					WHEN NO_DATA_FOUND THEN
						RAISE_APPLICATION_ERROR(-20000, 'Código do operador não encontrado.');	
				END;
			END IF;
			
			IF V_APT.APT_ATI_IN_CODIGO IS NULL THEN
				SELECT B.ATI_IN_CODIGOINT,
							 C.OPR_IN_CODIGO
				INTO   V_APT.APT_ATI_IN_CODIGO,
							 V_OPR_IN_CODIGO			
				FROM   MGMAN.PRO_ORDENS          A,
							 MGMAN.PRO_TIPOORDENS      B,
							 MGMAN.PRO_PROG_ORDEM      C
				WHERE  A.TPO_TAB_IN_CODIGO  = B.TPO_TAB_IN_CODIGO 
				AND    A.TPO_PAD_IN_CODIGO  = B.TPO_PAD_IN_CODIGO 
				AND    A.TPO_ST_CODIGO_TIPO = B.TPO_ST_CODIGO_TIPO						
				AND    A.ORG_TAB_IN_CODIGO  = C.ORG_TAB_IN_CODIGO
				AND    A.ORG_PAD_IN_CODIGO  = C.ORG_PAD_IN_CODIGO
				AND    A.ORG_IN_CODIGO      = C.ORG_IN_CODIGO    
				AND    A.ORG_TAU_ST_CODIGO  = C.ORG_TAU_ST_CODIGO
				AND    A.ORD_TAB_IN_CODIGO  = C.ORD_TAB_IN_CODIGO
				AND    A.ORD_SEQ_IN_CODIGO  = C.ORD_SEQ_IN_CODIGO
				AND    A.ORD_IN_CODIGO      = C.ORD_IN_CODIGO
				AND    C.ORG_TAB_IN_CODIGO  = V_APT.ORG_TAB_IN_CODIGO
				AND    C.ORG_PAD_IN_CODIGO  = V_APT.ORG_PAD_IN_CODIGO
				AND    C.ORG_IN_CODIGO      = V_APT.ORG_IN_CODIGO    
				AND    C.ORG_TAU_ST_CODIGO  = V_APT.ORG_TAU_ST_CODIGO
				AND    C.ORD_TAB_IN_CODIGO  = V_APT.ORD_TAB_IN_CODIGO
				AND    C.ORD_SEQ_IN_CODIGO  = V_APT.ORD_SEQ_IN_CODIGO
				AND    C.ORD_IN_CODIGO      = V_APT.ORD_IN_CODIGO
				AND    C.PLF_IN_SQOPERACAO  = V_APT.PLF_IN_SQOPERACAO
				;
			END IF;
			
			IF V_OPR_IN_CODIGO IS NOT NULL THEN 
				SELECT B.OPR_BO_APTELEUNICO,
							 B.OPR_BO_EDTTEMPOINICIAL,
							 B.OPR_BO_EDTTEMPOFINAL
				INTO   V_OPR_BO_APTELEUNICO,
							 V_OPR_BO_EDTTEMPOINICIAL,
							 V_OPR_BO_EDTTEMPOFINAL
				FROM   MGMAN.PRO_OPERACOES       A,
							 MGMAN.PRO_OPERACOESCMPESP B
				WHERE  A.OPR_TAB_IN_CODIGO = B.OPR_TAB_IN_CODIGO
				AND    A.OPR_PAD_IN_CODIGO = B.OPR_PAD_IN_CODIGO
				AND    A.OPR_IN_CODIGO     = B.OPR_IN_CODIGO    
				AND    A.OPR_IN_CODIGO     = V_OPR_IN_CODIGO;
			END IF;
			
			SELECT A.ATI_CH_PRODUTIVA
			INTO   V_ATI_CH_PRODUTIVA
			FROM   MGMAN.PRO_ATIVIDADE A
			WHERE  A.ATI_IN_CODIGO = V_APT.APT_ATI_IN_CODIGO;
							    
			IF (
					    (V_APT.APT_CH_STATUS = 'E')
 				  AND (V_ATI_CH_PRODUTIVA = 'P')
					AND (V_APT.APT_RE_QUANTIDADE = 0) 
					AND (V_APT.APT_RE_QUANTIDADEREF = 0)
			   )		 
		  THEN
          RAISE_APPLICATION_ERROR(-20000, 'O apontamento deve conter Quantidade Produzida e/ou Quantidade Refugada.');          				
			END IF;
			
  		IF ((V_APT.APT_CH_STATUS = 'E') OR (V_OPR_BO_APTELEUNICO = 'S') OR (V_APT.APT_IN_SEQUENCIA IS NOT NULL ) ) THEN           
					 V_APT.APT_RE_TEMPOFINAL   := MGMAN.MAN_PCK_UTIL.F_CONVERTESEGUNDOS(V_APT_HR_TEMPOFINAL || ':00');				   
					 V_APT.APT_RE_TEMPOTOTAL   := (TO_DATE(V_APT.APT_DT_ENCERRAMENTO || ' ' || V_APT_HR_TEMPOFINAL, 'DD/MM/YYYY  HH24:MI:SS') - TO_DATE(V_APT.APT_DT_APONTAMENTO || ' ' || V_APT_HR_TEMPOINICIAL, 'DD/MM/YYYY HH24:MI:SS')) * 24 * 60 * 60;
      ELSE
				  V_APT.APT_DT_ENCERRAMENTO := NULL;
				  V_APT.APT_RE_TEMPOFINAL   := NULL;			
      END IF;

			IF V_APT.APT_IN_SEQUENCIA IS NULL THEN       
			
				V_APT.APT_IN_SEQUENCIA    := MGCUSTOM.APT_SQ_APONTAORDEM.NEXTVAL;	
				V_APT.APT_RE_TEMPOINICIAL := MGMAN.MAN_PCK_UTIL.F_CONVERTESEGUNDOS(V_APT_HR_TEMPOINICIAL||':00');			
				INSERT INTO MGCUSTOM.APT_APONTAORDEM VALUES V_APT;			
			
			ELSE
						 			  
				SELECT A.APT_RE_TEMPOINICIAL
				INTO   V_APT.APT_RE_TEMPOINICIAL
				FROM   MGCUSTOM.APT_APONTAORDEM A
				WHERE  A.APT_IN_SEQUENCIA = V_APT.APT_IN_SEQUENCIA;
				
				--V_APT.APT_RE_TEMPOFINAL := MGMAN.MAN_PCK_UTIL.F_CONVERTESEGUNDOS(V_APT_HR_TEMPOFINAL);
				--V_APT.APT_RE_TEMPOTOTAL := V_APT.APT_RE_TEMPOFINAL - V_APT.APT_RE_TEMPOINICIAL;
			
				UPDATE MGCUSTOM.APT_APONTAORDEM A
				SET    A.APT_DT_ENCERRAMENTO  = V_APT.APT_DT_ENCERRAMENTO,
							 A.APT_RE_TEMPOFINAL    = V_APT.APT_RE_TEMPOFINAL,
							 A.APT_RE_TEMPOTOTAL    = V_APT.APT_RE_TEMPOTOTAL,
							 A.APT_RE_QUANTIDADE    = V_APT.APT_RE_QUANTIDADE,
							 A.APT_RE_QUANTIDADEREF = V_APT.APT_RE_QUANTIDADEREF,
							 A.APT_REF_IN_CODIGO    = V_APT.APT_REF_IN_CODIGO
							 --A.APT_CH_STATUS        = V_APT.APT_CH_STATUS
				WHERE  A.APT_IN_SEQUENCIA     = V_APT.APT_IN_SEQUENCIA;				
							
			END IF;
			
      IF (			     
					 (V_APT.APT_CH_STATUS = 'E')
			   )		 
		  THEN
				
				UPDATE MGCUSTOM.APT_APONTAORDEM A
				SET    A.APT_CH_STATUS = 'A'							 
				WHERE  A.APT_IN_SEQUENCIA = V_APT.APT_IN_SEQUENCIA;				
				
				MGCUSTOM.APT_PCK.P_ATUAPONTA_ESTOQUE(V_APT.APT_IN_SEQUENCIA);                    							
				
			END IF;			
			
 			V_CODIGO   := 0;
      V_MENSAGEM := 'OK';		
			COMMIT;					
			
		EXCEPTION
			WHEN OTHERS THEN
   			V_CODIGO   := SQLCODE;
        V_MENSAGEM := SQLERRM;
				OPEN P_RESULT FOR
				SELECT V_CODIGO   AS CODE,
				       V_MENSAGEM AS ERRM				       
				FROM   DUAL;
				ROLLBACK;
		END;
      				
		OPEN P_RESULT FOR
    SELECT V_CODIGO AS CODIGO,
		       V_MENSAGEM AS MENSAGEM
		FROM   DUAL
		;

  END;		
	
  PROCEDURE P_APONTAMENTO_DEL (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		
		V_APT_IN_SEQUENCIA MGCUSTOM.APT_APONTAORDEM.APT_IN_SEQUENCIA%TYPE;
	  V_APT_APO_IN_SEQUENCIA MGCUSTOM.APT_APONTAORDEM.APT_APO_IN_SEQUENCIA%TYPE;

		
		V_CODIGO   NUMBER;
    V_MENSAGEM VARCHAR2(500);
								
  BEGIN
    BEGIN
			
			V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
			SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/APT_IN_SEQUENCIA')
			INTO   V_APT_IN_SEQUENCIA
			FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));
			
      SELECT A.APT_APO_IN_SEQUENCIA 
			INTO   V_APT_APO_IN_SEQUENCIA
			FROM   MGCUSTOM.APT_APONTAORDEM A
			WHERE  A.APT_IN_SEQUENCIA = V_APT_IN_SEQUENCIA;

			IF V_APT_APO_IN_SEQUENCIA IS NOT NULL THEN
			  RAISE_APPLICATION_ERROR(-20000, 'Apontamento integrado, exclusão não permitida.');
  		END IF;
				    
      DELETE 
			FROM   MGCUSTOM.APT_APONTAORDEM A
			WHERE  A.APT_IN_SEQUENCIA = V_APT_IN_SEQUENCIA;

			V_CODIGO   := 0;
			V_MENSAGEM := 'OK';							
			
		EXCEPTION
			WHEN OTHERS THEN
   			V_CODIGO   := SQLCODE;
        V_MENSAGEM := SQLERRM;
				OPEN P_RESULT FOR
				SELECT V_CODIGO   AS CODE,
				       V_MENSAGEM AS ERRM				       
				FROM   DUAL;
		END;
      				
		OPEN P_RESULT FOR
    SELECT V_CODIGO AS CODIGO,
		       V_MENSAGEM AS MENSAGEM
		FROM   DUAL
		;

  END;		
		
  PROCEDURE P_CMAQUINA_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
  IS
    V_XML XMLTYPE;	
		V_ORG_IN_CODIGO      MGGLO.GLO_AGENTES.AGN_IN_CODIGO%TYPE;
		
		V_CTR_TAB_IN_CODIGO  MGMAN.PRO_PERTMAQUINAS.CTR_TAB_IN_CODIGO%TYPE;
    V_CTR_PAD_IN_CODIGO  MGMAN.PRO_PERTMAQUINAS.CTR_PAD_IN_CODIGO%TYPE;    
		V_CTR_IN_CODIGO      MGMAN.PRO_PERTMAQUINAS.CTR_IN_CODIGO%TYPE;
		
		V_CMAQ_IN_CODIGO     MGMAN.PRO_PERTMAQUINAS.CMAQ_IN_CODIGO%TYPE;
				
  BEGIN

    V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/ORG_IN_CODIGO'),
           EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CTR_IN_CODIGO'),
           EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/CMAQ_IN_CODIGO')					 					 
    INTO   V_ORG_IN_CODIGO,
           V_CTR_IN_CODIGO,
					 V_CMAQ_IN_CODIGO
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));
		
  	V_CTR_TAB_IN_CODIGO := 207;
    V_CTR_PAD_IN_CODIGO := MGGLO.PCK_MEGA.ACHAPADRAODATABELA(V_ORG_IN_CODIGO, V_CTR_TAB_IN_CODIGO, TRUNC(SYSDATE));
		
    OPEN P_RESULT FOR
    SELECT A.CTR_IN_CODIGO, 
		       A.CMAQ_IN_CODIGO,
					 B.CMAQ_ST_DESCRICAO
    FROM   MGMAN.PRO_PERTMAQUINAS A,
		       MGMAN.PRO_CADMAQUINAS  B
		WHERE  A.CMAQ_TAB_IN_CODIGO = B.CMAQ_TAB_IN_CODIGO
    AND    A.CMAQ_PAD_IN_CODIGO = B.CMAQ_PAD_IN_CODIGO
    AND    A.CMAQ_IN_CODIGO     = B.CMAQ_IN_CODIGO    
		AND    A.CTR_TAB_IN_CODIGO  = V_CTR_TAB_IN_CODIGO
		AND    A.CTR_PAD_IN_CODIGO  = V_CTR_PAD_IN_CODIGO
		AND    A.CTR_IN_CODIGO      = V_CTR_IN_CODIGO
		AND    A.CMAQ_IN_CODIGO     = NVL(V_CMAQ_IN_CODIGO, A.CMAQ_IN_CODIGO)
		;

  END;		
		
	PROCEDURE P_EVENTO_LST (
    P_PARAMS IN CLOB,
    P_RESULT OUT SYS_REFCURSOR
  )
	IS
  	V_XML XMLTYPE;
		V_EVENTO VARCHAR2(500);
		V_CODE   NUMBER;
    V_ERRM   VARCHAR2(500);
	BEGIN
	  
--		RAISE_APPLICATION_ERROR(-20000, 'aki');  
		
		V_XML := XMLTYPE.CREATEXML(UPPER(P_PARAMS));
    SELECT EXTRACTVALUE(COLUMN_VALUE,'/PARAMETROS/EVENTO')
    INTO   V_EVENTO           
    FROM   TABLE(XMLSEQUENCE(V_XML.EXTRACT('/PARAMETROS')));	

    BEGIN
			IF UPPER(V_EVENTO) = 'P_OPERADOR_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_OPERADOR_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);
			ELSIF UPPER(V_EVENTO) = 'P_OPERACAO_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_OPERACAO_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);
			ELSIF UPPER(V_EVENTO) = 'P_ORDEM_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_ORDEM_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);
			ELSIF UPPER(V_EVENTO) = 'P_APONTAMENTO_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_APONTAMENTO_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);
			ELSIF UPPER(V_EVENTO) = 'P_CMAQUINA_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_CMAQUINA_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);			
			ELSIF UPPER(V_EVENTO) = 'P_APONTAMENTO_EXE' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_APONTAMENTO_EXE(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);	
		  ELSIF UPPER(V_EVENTO) = 'P_APONTAMENTO_DEL' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_APONTAMENTO_DEL(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);		
      ELSIF UPPER(V_EVENTO) = 'P_REFUGO_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_REFUGO_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);	
      ELSIF UPPER(V_EVENTO) = 'P_ATIVIDADE_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_ATIVIDADE_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);	
      ELSIF UPPER(V_EVENTO) = 'P_MAQUINA_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_MAQUINA_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);	
      ELSIF UPPER(V_EVENTO) = 'P_ORDEM_OPERACAO_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_ORDEM_OPERACAO_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);	
      ELSIF UPPER(V_EVENTO) = 'P_MAQUINA_ORDEM_LST' THEN
				MGCUSTOM.IA_PCK_WS_MAN_PADRAO.P_MAQUINA_ORDEM_LST(
					P_PARAMS => P_PARAMS,
					P_RESULT => P_RESULT
				);					
																															
			END IF;						
		EXCEPTION
			WHEN OTHERS THEN
   			V_CODE := SQLCODE;
        V_ERRM := SQLERRM;
				OPEN P_RESULT FOR
				SELECT V_CODE AS CODE,
				       V_ERRM AS ERRM				       
				FROM   DUAL;
		END;														
		
  	COMMIT;
		
	END;

BEGIN
  NULL;
END IA_PCK_WS_MAN_PADRAO;
/
